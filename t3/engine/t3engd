#! /usr/bin/perl -Tw

use strict;

#use Barefoot::debug(1);						# comment out for production

use Fcntl;
use FileHandle;
use File::Basename;

use Barefoot::base;
use Barefoot::exception;
use Barefoot::T3::base;
use Barefoot::T3::Server;


# client doesn't have long to start reading the pipe they created
use constant TIMEOUT_CLIENT_STARTREAD => 1;

setup_modules();


# main ()

open(LOG, ">t3engd.log") or die("can't create log file");
LOG->autoflush();
log_error("starting server");

my $req_pipe = t3_create_pipe(T3::REQUEST_FILE);
unless ($req_pipe)
{
	log_error("can't create request pipe: $!");
	exit 1;
}


# the open will block until it receives a request
T3::debug("before initial open");
open_request_pipe();
T3::debug("after initial open");
while (1)
{
	my $request = <PIPE>;
	if (not $request)				# got EOF; close pipe and reopen
	{
		close(PIPE);
		open_request_pipe();
		next;
	}

	chomp($request);
	T3::debug("request is $request");
	if ($request eq "SHUTDOWN")
	{
		# handle shutdown requests right here
		shutdown_server();
	}
	else
	{
		# parse request string and test for errors
		my $req = parse_request_string($request);
		next unless $req;			# indicates error in parsing

		my $handler = $req->{request};
		T3::debug("request handler is $request");
		unless ($req->{output})
		{
			log_error("output pipe name invalid or missing");
			next;
		}

		T3::debug(3, "pipe name is $req->{output}");
		my $pipe;
		unless (timeout
				{
					$pipe = open_output_pipe($req->{output})
				}
				TIMEOUT_CLIENT_STARTREAD
		)
		{
			log_error("client didn't start reading in a timely fashion");
			next;
		}

		T3::debug(5, "got pipe handle");
		if ($pipe)
		{
			my $oldfh = select($pipe);
			$| = 1;

			my @input_lines;
			if ($req->{lines})
			{
				T3::debug("now reading $req->{lines} lines of input");
				for (1..$req->{lines})
				{
					my $line = <PIPE>;
					next unless $line;
					push @input_lines, $line;
				}
			}

			try
			{
				T3::Server::execute_request($handler, $req, @input_lines);
			}
			catch
			{
				log_error("request handler $handler failed: $_");
			};

			select($oldfh);
			$pipe->close() unless exists $req->{combine_next};
		}
		else
		{
			log_error("can't open output pipe [$req->{output}]");
		}
	}
}
# this really shouldn't be possible ...
die("popped out of infinite loop");



###########################
# Subroutines:
###########################


sub setup_modules
{
	my $mod_dir = t3_config(T3::MODULEDIR_DIRECTIVE);
	T3::debug("modules directory is $mod_dir");
	die("modules directory parameter not set in config file") unless $mod_dir;
	die("modules directory $mod_dir is not accessible") unless -d $mod_dir;

	# now make sure this directory looks secure
	# we're going to be untainting all filenames in this directory, so we
	# better be damned sure it's okay
	# first, make sure owner matches t3engd's owner
	die("modules directory is not owned by t3engd owner")
			unless (stat $0)[4] == (stat $mod_dir)[4];
	# now make sure owner is the one running the daemon
	die("modules directory is not owned by euid") unless -o $mod_dir;
	# now that the owner is cool, just make sure no one else has perms
	die("modules directory has too many permissions (should be 700 max)")
			if (stat $mod_dir)[2] & 077;

	foreach my $file (glob("$mod_dir/*.pm"))
	{
		# blatant untainting now that we're satisfied with the perms
		($file) = $file =~ /^(.*)$/;
		require $file;
	}
}


sub log_error
{
	my ($msg) = @_;

	print LOG "$0: $msg at ", scalar(localtime(time())), "\n";
}


sub open_request_pipe
{
	T3::debug(5, "about to open pipe $req_pipe");
	T3::debug(4, -p $req_pipe ? "file is a pipe" : "file is not a pipe");
	# T3::debug("fuser says " . `/usr/sbin/fuser -u ./t3.request`);

	# a regular open wasn't working here, but I don't know why ...
	# sysopen seems to have solved the problem (for now)
	sysopen(PIPE, $req_pipe, O_RDONLY)
			or die("can't open the request pipe for reading");
	T3::debug(5, "got pipe open");
}


sub parse_request_string
{
	my $req = {};
	foreach (split(' ', $_[0]))
	{
		if ( /(\w+)=(.*)/ )
		{
			T3::debug(2, "parse request: setting $1 to $2");
			$req->{$1} = $2;
		}
		else
		{
			log_error("malformed request [$_[0]]");
			return undef;
		}
	}

	# check for basic errors
	log_error("missing request handler name [$_[0]]") and return undef
			unless exists $req->{request};
	log_error("unknown request handler [$req->{request}]") and return undef
			unless T3::Server::exists_request($req->{request});

	return $req;
}


sub open_output_pipe
{
	my ($id) = @_;

	T3::debug(4, "pipe id is $id");
	if (not $id)
	{
		log_error("output pipe name invalid");
		return undef;
	}

	my $pipe_file = t3_pipename(T3::OUTPUT_FILE . $id);
	unless (-w $pipe_file and -p _)
	{
		log_error("client didn't create pipe for $id or wrong mode on pipe");
		return undef;
	}

	T3::debug(5, "about to open output pipe");
	return new FileHandle(">$pipe_file");
}


sub shutdown_server
{
	unlink($req_pipe) or log_error("couldn't remove input pipe");
	log_error("shutting down");
	exit 0;
}
